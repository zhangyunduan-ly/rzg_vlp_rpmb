#@TYPE: Machine
#@NAME: ly-rzg2l machine
#@DESCRIPTION: RZ/G2L LY platform

require conf/machine/include/arm/armv8-2a/tune-cortexa55.inc
require conf/machine/include/rz-common.inc
require conf/machine/include/rzg2l-family.inc

# Trusted Firmware-A configuration
PREFERRED_VERSION_trusted-firmware-a ?= "2.9%"
TFA_PLATFORM ?= "g2l"
TFA_BOARD ?= "smarc_1"
TFA_UBOOT ?= "1"
TFA_BUILD_TARGET ?= "bl2 bl31 fip"
BL2_BOOT_TARGET ?= "spi mmc esd"
TFA_PMIC_EXTRA_OEMAKE ?= " \
	BOARD=smarc_pmic_1 \
"
BL2_ADJUST_VMA ?= "0x00011E00"
FIP_ADJUST_VMA ?= "0x0000"

# U-Boot Configuration
UBOOT_CONFIG ??= "ly-rzg2l"
UBOOT_CONFIG[ly-rzg2l] = "ly-rzg2l_defconfig"
PREFERRED_VERSION_u-boot = "2021.10%"

# Linux kernel configuration
KERNEL_DEVICETREE = "renesas/ly-rzg2l-smarc.dtb"

# Flash Writer configuration
FLASH_WRITER_BOARD ?= "RZG2L_LY"
FLASH_WRITER_PMIC_BOARD ?= "RZG2L_LY_PMIC"

# Support board with PMIC. Default:yes.
# With this option enabled, build both bin files for PMIC board and Concrete board.
PMIC_SUPPORT ?= "1"

# Wic image
WKS_FILE = "${@oe.utils.conditional("PMIC_SUPPORT", "1", "rz-image-bootpart-esd-pmic.wks", "rz-image-bootpart-esd.wks", d)}"

IMAGE_BOOT_FILES = " \
	bl2_bp_spi-${MACHINE}_pmic.bin \
	bl2_bp_spi-${MACHINE}.bin \
	bl2_bp_mmc-${MACHINE}_pmic.bin \
	bl2_bp_mmc-${MACHINE}.bin \
	fip-${MACHINE}_pmic.bin \
	fip-${MACHINE}.bin \
	bl2_bp_spi-${MACHINE}_pmic.srec \
	bl2_bp_spi-${MACHINE}.srec \
	bl2_bp_mmc-${MACHINE}_pmic.srec \
	bl2_bp_mmc-${MACHINE}.srec \
	fip-${MACHINE}_pmic.srec \
	fip-${MACHINE}.srec \
	Flash_Writer_SCIF_RZG2L_LY_PMIC_DDR4_1GB_1PCS.mot \
"
do_image_wic[depends] += "firmware-pack:do_deploy flash-writer:do_deploy"

MACHINE_FEATURES:append = " mali-g31 hwh264dec hwh264enc"
